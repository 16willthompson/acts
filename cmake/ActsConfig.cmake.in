# CMake config for the Acts package
#
# Only defines CMake targets for requested and available components.
# No addititional variables are defined. All additional information, e.g.
# include directories and dependencies, are defined as target-specific
# properties and are automatically propagated when linking to the target.

@PACKAGE_INIT@

set(Acts_SUPPORTED_COMPONENTS @_supported_components@)
set(Acts_COMMIT_HASH "@_acts_commit_hash@")
set(Acts_COMMIT_HASH_SHORT "@_acts_commit_hash_short@")

# print version and components information
if(NOT Acts_FIND_QUIETLY)
  message(STATUS "found Acts version ${Acts_VERSION} - ${Acts_COMMIT_HASH_SHORT}")
  message(STATUS "supported components:")
  foreach(_component ${_supported_components})
    message(STATUS "  - ${_component}")
  endforeach()
endif()

# check that requested components are available
foreach(_component ${Acts_FIND_COMPONENTS})
  # check if this component is supported
  if(NOT _component IN_LIST Acts_SUPPORTED_COMPONENTS)
    if (${Acts_FIND_REQUIRED_${_component}})
      # not supported, but required -> fail
      set(Acts_FOUND False)
      message(FATAL_ERROR "required component \"${_component}\" not found")
    else()
      # not supported and optional -> skip
      list(REMOVE_ITEM Acts_FIND_COMPONENTS ${_component})
      if(NOT Acts_FIND_QUIETLY)
        message(STATUS "optional component \"${_component}\" not found")
      endif()
    endif()
  endif()
endforeach()

# load requested and available components
if(NOT Acts_FIND_QUIETLY)
  message(STATUS "loading components:")
endif()
foreach(_component ${Acts_FIND_COMPONENTS})
  if(NOT Acts_FIND_QUIETLY)
    message(STATUS "  - ${_component}")
  endif()
  # include the targets file to create the imported targets for the user
  include(${CMAKE_CURRENT_LIST_DIR}/Acts${_component}Targets.cmake)
endforeach()
